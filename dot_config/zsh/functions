function fzf-cdr () {
  local selected_dir="$(cdr -l | sed 's/^[0-9]\+ \+//' | fzf --reverse --query "$LBUFFER")"
  if [ -n "$selected_dir" ]; then
    BUFFER="cd `echo $selected_dir | awk '{print$2}'`"
    CURSOR=$#BUFFER
    zle accept-line
  else 
    zle reset-prompt
  fi
}
zle -N fzf-cdr
bindkey '^U' fzf-cdr

function fzf-history-selection() {
  BUFFER=`history -n 1 | tac  | awk '!a[$0]++' | fzf --reverse --query "$LBUFFER"`
  CURSOR=$#BUFFER
  zle reset-prompt
}

zle -N fzf-history-selection
bindkey '^R' fzf-history-selection

function find_in_files {
    local rg_prefix="rg --column --line-number --no-heading --color=always --smart-case "
    local initial_query="${*:-}"
    fzf --ansi --disabled --query "$initial_query" \
        --bind "start:reload:$rg_prefix {q}" \
        --bind "change:reload:sleep 0.1; $rg_prefix {q} || true" \
        --delimiter : \
        --preview 'bat --color=always {1} --highlight-line {2}' \
        --preview-window 'up,60%,border-bottom,+{2}+3/3,~3' \
        | awk -F':' '{print $1}'
}

function find_file_by_name {
  local query
  query="$1"

  local rg_cmd
  rg_cmd="rg --column --line-number --no-heading --color=always --smart-case"

  fzf --ansi \
      --delimiter ':' \
      --preview 'bat --style=numbers --color=always {1} --highlight-line {2}' \
      --preview-window 'right:60%' \
      --bind "change:reload:$rg_cmd {q} || true" \
      --bind "ctrl-f:reload:rg --files || true" \
      --bind "ctrl-r:reload:$rg_cmd {q} || true" \
      --header "Enter: open | Ctrl-F: file name | Ctrl-R: file content" \
      --prompt "rg> " \
  | awk -F: '{print $1 ":" $2}' \
  | while IFS=: read -r file line; do
        ${EDITOR:-vim} +"${line:-1}" "$file"
    done
}


